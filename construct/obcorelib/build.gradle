plugins {
	id 'org.springframework.boot' version '3.1.1'
	id 'io.spring.dependency-management' version "1.1.0"
	id 'java'
	id 'idea'
	id("maven-publish")
}


repositories {
	mavenCentral()
}

dependencyManagement {

	imports {
		mavenBom 'org.springframework.boot:spring-boot-dependencies:3.1.1'
	}

	dependencies {

	}
}

sourceCompatibility = 17
targetCompatibility = 17

dependencies {
	implementation("org.springframework.boot:spring-boot-starter-web")
	implementation('org.springframework.boot:spring-boot-starter-data-jpa')
	implementation('org.springframework.boot:spring-boot-starter-data-rest')
	implementation group: 'org.springframework.boot', name: 'spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
	implementation 'org.springframework.boot:spring-boot-starter-webflux' // For WebClient
	implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
	implementation "com.fasterxml.jackson.core:jackson-core"
	implementation "com.fasterxml.jackson.core:jackson-databind"
	implementation "com.fasterxml.jackson.datatype:jackson-datatype-json-org"
	implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310"
	implementation group: "org.apache.commons", name: "commons-lang3", version: "3.12.0"
	implementation group: "org.apache.commons", name: "commons-text", version: "1.+"
	implementation group: "org.apache.commons", name: "commons-collections4", version: "4.4"
	implementation group: 'org.postgresql', name: 'postgresql', version: '42.6.0'
	implementation group: 'com.microsoft.sqlserver', name: 'mssql-jdbc', version: '11.2.3.jre17'
	testImplementation('org.springframework.boot:spring-boot-starter-test')
	testImplementation('org.springframework.security:spring-security-test')
	implementation('org.springframework.boot:spring-boot-starter-log4j2')
}



configurations {
  all*.exclude group:'org.springframework.boot',
               module:'spring-boot-starter-logging'
	tests
}

bootJar {
  enabled = false
}

test {
	useJUnitPlatform()
}


task srcZip (type: Zip) {

	archiveClassifier = 'src'
	from projectDir
	include 'src/**/*'
	
}

semantic_version = project.findProperty("semantic_version") ?: System.getenv("SEMANTIC_VERSION")

jar {
	enabled = true
	archiveClassifier = ''
    archiveBaseName = 'obcorelib'
	manifest {
			attributes("Implementation-Title": "OnBelay Core")
	}
}

task construct(type: Copy, dependsOn: assemble) {

	from 'build/libs'
	include '*.jar'
	into 'c:/softdev/repos/libs'
}

task testJar(type: Jar, dependsOn: testClasses) {
	archiveClassifier.set('tests')
	from sourceSets.test.output
}


task runUnitTests(type: Test) {
	jvmArgs '-Xmx2G', '-Xms512m', '-Duser.timezone=GMT-7'
	testLogging.exceptionFormat "full" //produces a little more information in the output, mainly useful for development
	scanForTestClasses = false  //this turns off the auto discovery feature which causes all kinds of false failures

	include "**/*Test.class"

	exclude "**/Base*.class" //have no tests in them
	exclude "**/TransactionalSpringTestCase.class" //has no tests in it

	testLogging {
		int numTestsPerformed = 0
		afterTest { desc, result ->
			numTestsPerformed++
			if (numTestsPerformed % 100 == 0) {
				println('Still Testing... finished ' + numTestsPerformed + ' tests')
			}
		}
	}

}



artifacts {
	tests testJar
}


publishing {
	repositories {
		maven {
			name = "GitHubPackages"
			url = uri("https://maven.pkg.github.com/onbelayconsulting/obcorelib")
			credentials {
				username = project.findProperty("gpr.user") ?: System.getenv("GPR_USER")
				password = project.findProperty("gpr.key") ?: System.getenv("GPR_TOKEN")
			}
		}
	}
	publications {
		obcorelibrary(MavenPublication) {
			groupId = 'com.onbelay.core'
			artifactId = 'obcore'
			version = System.getenv("SEMANTIC_VERSION") ?: project.findProperty("semantic_version")
			artifact jar
		}
		obcorelibraryTest(MavenPublication) {
			groupId = 'com.onbelay.core'
			artifactId = 'obcoretest'
			version = System.getenv("SEMANTIC_VERSION") ?: project.findProperty("semantic_version")
			artifact testJar
		}
	}
}

